plugins {
    id "net.minecrell.licenser" version "0.4.1"
    id "net.researchgate.release" version "2.8.1"
    id "com.techshroom.incise-blue" version "0.5.7"
}

apply plugin: 'java'
apply plugin: 'java-library'
apply plugin: 'eclipse'
apply plugin: 'application'

mainClassName = 'me.kenzierocks.ourtube.OurTube'

inciseBlue {
    ide()
    license()
    util {
        javaVersion = JavaVersion.VERSION_14
    }
}

license {
    exclude '**/me/kenzierocks/ourtube/netty/SimpleHttpServer.java'
}

def JACKSON = '2.10.3'
configurations.all {
    resolutionStrategy {
        cacheDynamicVersionsFor 10, 'minutes'
        // don't cache changing modules at all
        cacheChangingModulesFor 0, 'seconds'
    }
}

dependencies {
    implementation(platform("com.fasterxml.jackson:jackson-bom:$JACKSON"))
    implementation group: 'org.slf4j', name: 'slf4j-api', version: '1.7.30'
    implementation group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.3'
    implementation group: 'ch.qos.logback', name: 'logback-core', version: '1.2.3'
    implementation 'com.discord4j:discord4j-core:3.0.14-SNAPSHOT'
    implementation 'com.sedmelluq:lavaplayer:1.3.43'
    implementation group: 'io.netty', name: 'netty-all', version: '4.1.48.Final'

    implementation 'org.bytedeco.javacpp-presets:ffmpeg-platform:4.1-1.4.4'
    implementation group: 'com.squareup.okhttp3', name: 'okhttp', version: '4.4.1'

    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jdk8'
    implementation('com.fasterxml.jackson.datatype:jackson-datatype-guava') {
        exclude(group: "com.google.guava", module: "guava")
    }

    implementation 'com.google.api-client:google-api-client:1.30.9'
    implementation 'com.google.oauth-client:google-oauth-client-jetty:1.30.6'
    implementation 'com.google.apis:google-api-services-youtube:v3-rev20200312-1.30.9'

    compileOnly group: 'com.techshroom', name: 'jsr305-plus', version: '0.0.1'

    implementation group: 'com.google.guava', name: 'guava'
    constraints {
        implementation(group: 'com.google.guava', name: 'guava') {
            version {
                // guava is weird and has -android which we don't want
                strictly("28.2-jre")
            }
            because("it's what we're using")
        }
    }
    def autoService = "1.0-rc6"
    compileOnly group: 'com.google.auto.service', name: 'auto-service-annotations', version: autoService
    annotationProcessor group: 'com.google.auto.service', name: 'auto-service', version: autoService
    def autoValue = "1.7"
    compileOnly group: 'com.google.auto.value', name: 'auto-value-annotations', version: autoValue
    annotationProcessor group: 'com.google.auto.value', name: 'auto-value', version: autoValue

    testImplementation group: 'junit', name: 'junit', version: '4.13'

}

javadoc {
    options.addStringOption('Xdoclint:none', '-quiet')
}

configure([compileJava, compileTestJava]) {
    options.compilerArgs += ['-Xlint:all', '-Xlint:-processing', '-Xlint:-path']
    options.deprecation = true
    options.encoding = 'UTF-8'
    options.incremental = true
    options.fork = true
}
