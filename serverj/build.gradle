plugins {
    id "com.github.hierynomus.license" version "0.14.0"
 	id "net.researchgate.release" version "2.7.0"
 	id "com.techshroom.incise-blue" version "0.0.8"
}

apply plugin: 'java'
apply plugin: 'java-library'
apply plugin: 'eclipse'
apply plugin: 'application'

mainClassName = 'me.kenzierocks.ourtube.OurTube'

inciseBlue {
    plugins {
    	apt
    	license
    	util
    }
    
    util.javaVersion = '1.8'
}

license {
    exclude '**/me/kenzierocks/ourtube/netty/SimpleHttpServer.java'
}

def JACKSON = '2.9.5'
configurations.all {
    resolutionStrategy {
    	// fail eagerly on version conflict (includes transitive dependencies)
    	// e.g. multiple different versions of the same dependency (group and name are equal)
    	failOnVersionConflict()

    	// force certain versions of dependencies (including transitive)
    	force 'com.google.guava:guava:24.1-jre', 'com.google.code.findbugs:jsr305:3.0.1',
        	'org.apache.httpcomponents:httpcore:4.4.8', 'junit:junit:4.12',
        	'org.slf4j:slf4j-api:1.7.25', 'com.fasterxml.jackson.core:jackson-core:' + JACKSON,
        	'com.fasterxml.jackson.core:jackson-databind:' + JACKSON, 
            'org.apache.httpcomponents:httpclient:4.5.4',
        	'org.apache.commons:commons-lang3:3.7', 'commons-io:commons-io:2.6'

     	cacheDynamicVersionsFor 10, 'minutes'
     	// don't cache changing modules at all
     	cacheChangingModulesFor 0, 'seconds'
   	}
}

dependencies {
    implementation group: 'org.slf4j', name: 'slf4j-api', version: '1.7.25'
    implementation group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.3'
    implementation group: 'ch.qos.logback', name: 'logback-core', version: '1.2.3'
    implementation 'com.discord4j:Discord4J:2.10.1'
    implementation 'com.sedmelluq:lavaplayer:1.2.63'
    implementation group: 'io.netty', name: 'netty-all', version: '4.1.25.Final'

    implementation 'org.bytedeco.javacpp-presets:ffmpeg-platform:3.4.2-1.4.1'
    implementation group: 'com.squareup.okhttp3', name: 'okhttp', version: '3.10.0'
    
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jdk8:' + JACKSON
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-guava:' + JACKSON

    implementation 'com.google.api-client:google-api-client:1.23.0'
    implementation 'com.google.oauth-client:google-oauth-client-jetty:1.23.0'
    implementation 'com.google.apis:google-api-services-youtube:v3-rev189-1.23.0'

    compileOnly group: 'com.techshroom', name: 'jsr305-plus', version: '0.0.1'

    implementation group: 'com.google.guava', name: 'guava', version: '24.1-jre'
    aptCompileOnly group: 'com.google.auto.service', name: 'auto-service', version: '1.0-rc4'
    compileOnly group: 'com.google.auto.value', name: 'auto-value-annotations', version: '1.6'
    annotationProcessor group: 'com.google.auto.value', name: 'auto-value', version: '1.6'

    testImplementation group: 'junit', name: 'junit', version: '4.12'

}

javadoc {
    options.addStringOption('Xdoclint:none', '-quiet')
}

configure([compileJava, compileTestJava]) {
    options.compilerArgs += ['-Xlint:all', '-Xlint:-processing', '-Xlint:-path']
    options.deprecation = true
    options.encoding = 'UTF-8'
    options.incremental = true
    options.fork = true
}
