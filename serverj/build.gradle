buildscript {
    repositories {
        mavenCentral()
        maven {
            url "https://oss.sonatype.org/content/repositories/snapshots/"
        }
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }

    configurations.all {
        resolutionStrategy {
            cacheChangingModulesFor 0, 'seconds'
        }
    }
    dependencies {
        classpath group: 'com.techshroom', name: 'Aversion', version: '3.0.1'
    }
}
plugins {
    id "com.github.hierynomus.license" version "0.14.0"
 	id "net.researchgate.release" version "2.6.0"
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'aversion-util'
apply plugin: 'aversion-apt'
apply plugin: 'application'

mainClassName = 'me.kenzierocks.ourtube.OurTube'

license {
    ext {
        name = rootProject.name
        organization = rootProject.organization
        url = rootProject.url
    }
    header = rootProject.file('HEADER.txt')
    ignoreFailures = false
    strictCheck = true
    include '**/*.java'
    mapping {
        java = 'SLASHSTAR_STYLE'
    }
}

util {
    javaVersion = '1.8'
}

repositories {
    jcenter()
    maven {
        url "https://jitpack.io"
    }
    mavenCentral()
    maven {
        url "https://oss.sonatype.org/content/repositories/snapshots/"
    }
}
def JACKSON = '2.9.3'
configurations.all {
   resolutionStrategy {
     // fail eagerly on version conflict (includes transitive dependencies)
     // e.g. multiple different versions of the same dependency (group and name are equal)
     failOnVersionConflict()

     // force certain versions of dependencies (including transitive)
    force 'com.google.guava:guava:23.6-jre', 'com.google.code.findbugs:jsr305:3.0.1',
        'org.apache.httpcomponents:httpcore:4.4.8', 'junit:junit:4.12',
        'org.slf4j:slf4j-api:1.7.25', 'com.fasterxml.jackson.core:jackson-core:' + JACKSON,
        'com.fasterxml.jackson.core:jackson-databind:' + JACKSON, 'io.netty:netty-buffer:4.1.15.Final',
        'io.netty:netty-transport:4.1.15.Final', 'org.apache.httpcomponents:httpclient:4.5.4',
        'org.apache.commons:commons-lang3:3.7', 'commons-io:commons-io:2.6'

     cacheDynamicVersionsFor 10, 'minutes'
     // don't cache changing modules at all
     cacheChangingModulesFor 0, 'seconds'
   }
}

dependencies {
    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.25'
    compile group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.3'
    compile group: 'ch.qos.logback', name: 'logback-core', version: '1.2.3'
    compile 'com.github.austinv11:Discord4J:2.9.3'
    compile "com.corundumstudio.socketio:netty-socketio:1.7.13"
    compile 'com.github.axet:vget:1.1.34'
    compile 'org.bytedeco.javacpp-presets:ffmpeg-platform:3.2.1-1.3'
    
    compile 'com.fasterxml.jackson.datatype:jackson-datatype-jdk8:' + JACKSON
    compile 'com.fasterxml.jackson.datatype:jackson-datatype-guava:' + JACKSON

    compile 'com.google.api-client:google-api-client:1.23.0'
    compile 'com.google.oauth-client:google-oauth-client-jetty:1.23.0'
    compile 'com.google.apis:google-api-services-youtube:v3-rev189-1.23.0'

    compileOnly group: 'com.techshroom', name: 'jsr305-plus', version: '0.0.1'

    addAPTReqWComp group: 'com.google.guava', name: 'guava', version: '23.6-jre'
    addAPT group: 'com.google.auto.service', name: 'auto-service', version: '1.0-rc3'
    addAPT group: 'com.google.auto.value', name: 'auto-value', version: '1.5.2'
    addAPTReq name: 'aopalliance'
    addAPTReq name: 'asm'
    addAPTReq name: 'auto-value'
    addAPTReq name: 'auto-common'
    addAPTReq name: 'cglib'
    addAPTReq name: 'commons-collections'
    addAPTReq name: 'commons-lang'
    addAPTReq name: 'dagger'
    addAPTReq name: 'javawriter'
    addAPTReq name: 'javax.inject'
    addAPTReq name: 'velocity'

    testCompile group: 'junit', name: 'junit', version: '4.12'

}

javadoc {
    options.addStringOption('Xdoclint:none', '-quiet')
}

configure([compileJava, compileTestJava]) {
    options.compilerArgs += ['-Xlint:all', '-Xlint:-processing', '-Xlint:-path']
    options.deprecation = true
    options.encoding = 'UTF-8'
    options.incremental = true
    options.fork = true
}
